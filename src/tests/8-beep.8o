# Beep test

# This is a new test for this test suite, that allows you to test if your buzzer
# is working. It will beep SOS in morse code and flash a speaker icon on the
# display in the same pattern.

: main
  i := pattern
  load v0
  v2 := 1
  v3 := v0
  loop
    i := pattern
    i += v2
    load v1
    delay := v0
    draw-beep
    buzzer := v0
    check-for-manual-while-delay
    draw-beep
    delay := v1
    check-for-manual-while-delay
    v2 += 2
    if v2 < v3 then
  again
  jump main

: draw-beep
  i := speaker
  vA := 28
  vB := 12
  sprite vA vB 7
  return

: check-for-manual-while-delay
  loop
    vF := 0xB
    if vF key then jump manual-control
    v0 := delay
    if v0 == 0 then return
  again

: manual-control
  v0 := 0
  v1 := 60
  v2 := 0xB
  draw-beep
  loop
    buzzer := v1
    if v2 key then
  again
  draw-beep
  loop
    buzzer := v0
    if v2 -key then
  again
  jump manual-control

:segment data

: pattern
  18
  10 5 10 5 10 20
  30 5 30 5 30 20
  10 5 10 5 10 60

: speaker
  0b00011001
  0b00101010
  0b11001000
  0b10001011
  0b11001000
  0b00101010
  0b00011001

